PREFIX schema: <http://schemaorg/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <http://example.com/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX ceon-actorODP: <http://w3id.org/CEON/ontology/actorODP/>
PREFIX ceon-resourceODP: <http://w3id.org/CEON/ontology/resourceODP/>
PREFIX ceon-product: <http://w3id.org/CEON/ontology/product/>
PREFIX ceon-provenance: <http://w3id.org/CEON/ontology/provenance/>
PREFIX ceon-material: <http://w3id.org/CEON/ontology/material/>
PREFIX ceon-processODP: <http://w3id.org/CEON/ontology/processODP/>
PREFIX ceon-process:  <http://w3id.org/CEON/ontology/process/>
PREFIX textile:  <http://w3id.org/CEON/demo/textile/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX qudt-unit: <http://qudt.org/vocab/unit/>
PREFIX ceon-quantity: <http://w3id.org/CEON/ontology/quantity/>
PREFIX ceon-actor: <http://w3id.org/CEON/ontology/actor/>
PREFIX qudt-quantitykind: <http://qudt.org/vocab/quantitykind/>
PREFIX construction: <http://w3id.org/CEON/demo/construction/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX bot: <https://w3id.org/bot#>

SELECT DISTINCT ?ElementLabel ?ParentElementLabel ?SpaceLabel ?StoreyLabel ?AreaM2 ?InstallationDate ?ReusePotential ?DismantlingInstruction_url ?C2CMHSilverCertificate_url ?TakeBackProgramFromOriginalManufacturer
    ?DismantlerLabel ?DismantlingCostEuro ?TransporterLabel ?TransportCostEuro ?RecyclerLabel ?RecyclingCostEuro
WHERE {
    ?Building a bot:Building;
    rdfs:label $BuildingLabel.
    OPTIONAL {
        ?Building bot:hasStorey ?Storey .
        ?Storey rdfs:label ?StoreyLabel .
        OPTIONAL{
            ?Storey bot:hasSpace ?Space .
            ?Space rdfs:label ?SpaceLabel .
            OPTIONAL {
                ?Space bot:containsElement ?Element.
                ?Element rdfs:label ?ElementLabel.
                ?Element ceon-product:modelledBy ?ProductModelId .
                ?ProductModelId rdfs:label ?ProductModelLabel.
                OPTIONAL { ?ProductModelId construction:hasTakeBackProgramFromOriginalManufacturer ?TakeBackProgramFromOriginalManufacturer }
                OPTIONAL { ?ProductModelId ex:hasReusePotential ?ReusePotential }
                OPTIONAL { ?ProductModelId construction:hasDismantlingInstruction ?DismantlingInstruction_url }
                OPTIONAL {
                    ?ProductModelId construction:hasCompliance ?Compliance.
                    ?Compliance ceon-product:Compliance construction:C2C_MH_Silver ;
                    ceon-product:hasCertificate ?C2CMHSilverCertificate_url .
                }
                ?Installation construction:hasInstallationDate ?InstallationDate;
                construction:hasDimension ?AreaDimension ;
                ceon-actorODP:participatingResource ?Element ;
                construction:installationLocation ?Space .
                ?AreaDimension a qudt-quantitykind:Area;
                qudt:numericValue ?AreaM2
                OPTIONAL {
                    ?ParentElement ceon-product:hasProductComponent ?Element;
                        rdfs:label ?ParentElementLabel.
                }
                OPTIONAL {
                    ?DismantlingProcess ceon-actorODP:participatingResource ?Element .
                    ?DismantlingProcess ceon-actorODP:participatingActor ?Dismantler .
                    ?DismantlingProcess ceon-quantity:hasDismantlingCost ?DismantlingCost .
                    ?DismantlingCost qudt:numericValue ?DismantlingCostEuro .
                    ?Dismantler rdfs:label ?DismantlerLabel.
                }
                OPTIONAL {
                    ?TransportProcess ceon-actorODP:participatingResource ?Element .
                    ?TransportProcess ceon-actorODP:participatingActor ?Transporter .
                    ?TransportProcess ceon-quantity:hasTransportCost ?TransportCost .
                    ?TransportCost qudt:numericValue ?TransportCostEuro .
                    ?Transporter rdfs:label ?TransporterLabel.
                }
                OPTIONAL {
                    ?RecyclingProcess ceon-actorODP:participatingResource ?Element .
                    ?RecyclingProcess ceon-actorODP:participatingActor ?Recycler .
                    ?RecyclingProcess ceon-quantity:hasRecyclingCost ?RecyclingCost .
                    ?RecyclingCost qudt:numericValue ?RecyclingCostEuro .
                    ?Recycler rdfs:label ?RecyclerLabel.
                }
            }
        }
    }
}

