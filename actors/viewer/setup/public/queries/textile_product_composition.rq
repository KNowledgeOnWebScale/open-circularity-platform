PREFIX schema: <http://schemaorg/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <http://example.com/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX ceon-actorODP: <http://w3id.org/CEON/ontology/actorODP/>
PREFIX ceon-resourceODP: <http://w3id.org/CEON/ontology/resourceODP/>
PREFIX ceon-product: <http://w3id.org/CEON/ontology/product/>
PREFIX ceon-provenance: <http://w3id.org/CEON/ontology/provenance/>
PREFIX ceon-material: <http://w3id.org/CEON/ontology/material/>
PREFIX ceon-processODP: <http://w3id.org/CEON/ontology/processODP/>
PREFIX ceon-process:  <http://w3id.org/CEON/ontology/process/>
PREFIX textile:  <http://w3id.org/CEON/demo/textile/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?ComponentLabel ?MassFraction ?Origin ?Color ?DyeMethod ?DyeStuff ?FinishingStep ?WaterProperty ?ChemicalCompliance
?IncludesTrims ?ContainsMetal ?ProductDesignForRecycling ?REACHCertificate_url ?MSDSCertificate_url ?BiodegradabilityTest_url
WHERE {
    ?Product rdfs:label $ProductLabel .
    ?Composition a ceon-product:ProductComposition;
        ceon-product:compositionOf ?Product;
        qudt:numericValue ?MassFraction;
        ceon-product:associatedWithProductModel ?Component.
    ?Component rdfs:label ?ComponentLabel.
    OPTIONAL {?Component textile:hasOrigin ?Origin}
    OPTIONAL {?Component textile:hasColor ?Color_url}
    BIND (REPLACE(STR(?Color_url), "^.*/([^/]*)$", "$1") as ?Color)
    OPTIONAL {?Component textile:hasDyeMethod ?DyeMethod_url}
    BIND (REPLACE(STR(?DyeMethod_url), "^.*/([^/]*)$", "$1") as ?DyeMethod)
    OPTIONAL {?Component textile:hasDyeStuff ?DyeStuff_url}
    BIND (REPLACE(STR(?DyeStuff_url), "^.*/([^/]*)$", "$1") as ?DyeStuff)
    OPTIONAL {?Component textile:hasFinishingStep ?FinishingStep_url}
    BIND (REPLACE(STR(?FinishingStep_url), "^.*/([^/]*)$", "$1") as ?FinishingStep)
    OPTIONAL {?Component textile:hasWaterProperty ?WaterProperty_url}
    BIND (REPLACE(STR(?WaterProperty_url), "^.*/([^/]*)$", "$1") as ?WaterProperty)
    OPTIONAL {?Component textile:chemicalCompliance ?ChemicalCompliance}
    OPTIONAL {?Component textile:includesTrims ?IncludesTrims}
    OPTIONAL {?Component textile:containsMetal ?ContainsMetal}
    OPTIONAL {?Component textile:textileProductDesignForRecycling ?ProductDesignForRecycling_url}
    BIND (REPLACE(STR(?ProductDesignForRecycling_url), "^.*/([^/]*)$", "$1") as ?ProductDesignForRecycling)
    OPTIONAL {
        ?s_reach ceon-provenance:statementAbout ?Component;
        ceon-product:complianceWith textile:REACH;
        ceon-product:hasCertificate ?REACHCertificate_url.
    }
    OPTIONAL {
        ?s_msds ceon-provenance:statementAbout ?Component;
        ceon-product:complianceWith textile:MSDS;
        ceon-product:hasCertificate ?MSDSCertificate_url.
    }
    OPTIONAL {
        ?s_msds ceon-provenance:statementAbout ?Component;
        textile:biodegradabilityTestAvailable "true"^^xsd:boolean;
        ceon-product:hasCertificate ?BiodegradabilityTest_url.
    }
}
