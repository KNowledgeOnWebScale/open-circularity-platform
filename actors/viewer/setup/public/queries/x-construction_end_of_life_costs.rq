PREFIX schema: <http://schemaorg/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <http://example.com/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX ceon-actorODP: <http://w3id.org/CEON/ontology/actorODP/>
PREFIX ceon-resourceODP: <http://w3id.org/CEON/ontology/resourceODP/>
PREFIX ceon-product: <http://w3id.org/CEON/ontology/product/>
PREFIX ceon-provenance: <http://w3id.org/CEON/ontology/provenance/>
PREFIX ceon-material: <http://w3id.org/CEON/ontology/material/>
PREFIX ceon-processODP: <http://w3id.org/CEON/ontology/processODP/>
PREFIX ceon-process:  <http://w3id.org/CEON/ontology/process/>
PREFIX textile:  <http://w3id.org/CEON/demo/textile/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX qudt-unit: <http://qudt.org/vocab/unit/>
PREFIX ceon-quantity: <http://w3id.org/CEON/ontology/quantity/>
PREFIX ceon-actor: <http://w3id.org/CEON/ontology/actor/>
PREFIX qudt-quantitykind: <http://qudt.org/vocab/quantitykind/>
PREFIX construction: <http://w3id.org/CEON/demo/construction/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX bot: <https://w3id.org/bot#>
PREFIX missingvoc: <http://missingvoc.com#>
SELECT DISTINCT
?Element ?DismantlerLabel ?DismantlingCostEuro ?RecyclerLabel ?RecyclingCostEuro ?TakingBackCompanyLabel ?TakingBackPriceEuro
WHERE {
  ?Space bot:containsElement ?Element.
  ?Element rdfs:label ?ElementLabel.

  OPTIONAL {
    ?DismantlingProcessParticipation ceon-actorODP:participationIn ?DismantlingProcess .
    ?DismantlingProcess a ceon-process:DismantlingProcess;
    ceon-processODP:hasInput ?Element.
    ?DismantlingProcessParticipation ceon-actorODP:participatingActor ?Dismantler .
    ?DismantlingProcessParticipation ceon-quantity:hasDismantlingCost ?DismantlingCost .
    ?DismantlingCost qudt:numericValue ?DismantlingCostEuro;
    qudt:hasUnit qudt-unit:Euro.
    ?Dismantler rdfs:label ?DismantlerLabel.
  }
  OPTIONAL {
    ?RecyclingProcessParticipation ceon-actorODP:participationIn ?RecyclingProcess .
    ?RecyclingProcess a ceon-process:RecyclingProcess;
    ceon-processODP:hasInput ?Element.
    ?RecyclingProcessParticipation ceon-actorODP:participatingActor ?Recycler .
    ?RecyclingProcessParticipation ceon-quantity:hasRecyclingCost ?RecyclingCost .
    ?RecyclingCost qudt:numericValue ?RecyclingCostEuro ;
    qudt:hasUnit qudt-unit:Euro.
    ?Recycler rdfs:label ?RecyclerLabel.
  }
  OPTIONAL {
    ?TakingBackResource a ceon-actor:TakingBackResource ;
    ceon-actorODP:participatingActor ?TakingBackCompany ;
    ceon-actorODP:participatingResource ?Element ;
    ceon-quantity:hasPrice ?TakingBackPrice .
    ?TakingBackPrice qudt:numericValue ?TakingBackPriceEuro ;
    qudt:hasUnit qudt-unit:Euro.
    ?TakingBackCompany rdfs:label ?TakingBackCompanyLabel.
  }
}

