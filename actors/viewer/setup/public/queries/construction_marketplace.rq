PREFIX schema: <http://schemaorg/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <http://example.com/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX ceon-actorODP: <http://w3id.org/CEON/ontology/actorODP/>
PREFIX ceon-resourceODP: <http://w3id.org/CEON/ontology/resourceODP/>
PREFIX ceon-product: <http://w3id.org/CEON/ontology/product/>
PREFIX ceon-provenance: <http://w3id.org/CEON/ontology/provenance/>
PREFIX ceon-material: <http://w3id.org/CEON/ontology/material/>
PREFIX ceon-processODP: <http://w3id.org/CEON/ontology/processODP/>
PREFIX ceon-process:  <http://w3id.org/CEON/ontology/process/>
PREFIX textile:  <http://w3id.org/CEON/demo/textile/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX qudt-unit: <http://qudt.org/vocab/unit/>
PREFIX ceon-quantity: <http://w3id.org/CEON/ontology/quantity/>
PREFIX ceon-actor: <http://w3id.org/CEON/ontology/actor/>
PREFIX qudt-quantitykind: <http://qudt.org/vocab/quantitykind/>
PREFIX construction: <http://w3id.org/CEON/demo/construction/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX bot: <https://w3id.org/bot#>

SELECT DISTINCT ?BatchLabel ?BatchSize ?Condition ?ProductModelLabel ?C2CMHSilverCertificate_url ?OwnerLabel
?Thickness ?Length ?Width ?SellingPriceEuro
WHERE {
    ?Batch rdfs:label ?BatchLabel.
    ?Batch ceon-product:modelledBy ?ProductModelId .
    ?ProductModelId rdfs:label ?ProductModelLabel.
    ?Batch ceon-resourceODP:batchSize ?BatchSize.
    ?Batch construction:hasCondition ?Condition_url.
    BIND (REPLACE(STR(?Condition_url), "^.*/([^/]*)$", "$1") as ?Condition)
    OPTIONAL {
        ?ProductModelId construction:hasCompliance ?Compliance.
        ?Compliance ceon-product:Compliance construction:C2C_MH_Silver ;
        ceon-product:hasCertificate ?C2CMHSilverCertificate_url .
    }
    OPTIONAL {
        ?Batch construction:dimension ?LengthDimension.
        ?LengthDimension a qudt-quantitykind:Length;
        qudt:numericValue ?Length.
    }
    OPTIONAL {
        ?Batch construction:dimension ?WidthDimension.
        ?WidthDimension a qudt-quantitykind:Width;
        qudt:numericValue ?Width.
    }
    OPTIONAL {
        ?Batch construction:hasThickness ?ThicknessDimension.
        ?ThicknessDimension qudt:numericValue ?Thickness.
    }
    OPTIONAL {
        ?Batch ceon-quantity:hasMarketValue ?MarketValue.
        ?MarketValue qudt:numericValue ?SellingPriceEuro.
    }
    ?OwningResource a ceon-actor:OwningResource ;
    ceon-actorODP:participantRole ceon-actor:owner ;
    ceon-actorODP:participatingActor ?Owner ;
    ceon-actorODP:participatingResource ?Batch.
    ?Owner rdfs:label ?OwnerLabel .
}
