# What is the recycled content (weight percentage) of product p01?
# Datasources: ${OD_CSS6_BASE_URL}/manufacturer/data/dt/out/products.ttl ${OD_CSS6_BASE_URL}/manufacturer/data/dt/out/boms.ttl ${OD_CSS5_BASE_URL}/texon/data/dt/out/components.ttl

PREFIX oo: <http://purl.org/openorg/>
PREFIX ao: <http://purl.org/ontology/ao/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX d: <https://www.example.com/textile-data/>
PREFIX o: <https://www.example.com/textile-ont/>

SELECT ?product ?productName (SUM(?partialRecycledContentPercentage) as ?productRecycledContentPercentage)
WHERE {
  VALUES ?product {
    <https://www.example.com/textile-data/product-p01>
  }
  ?product
    a o:Product;
    o:name ?productName;
    o:has-product-bom [
      o:has-product-component-assoc [
        o:percentage ?percentage ;
        o:has-component [
          o:recycled-content-percentage ?recycledContentPercentage ;
        ];
      ];
    ];
  BIND (?percentage * ?recycledContentPercentage / 100 AS ?partialRecycledContentPercentage)
}
GROUP BY ?product ?productName
